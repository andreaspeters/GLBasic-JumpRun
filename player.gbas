// --------------------------------- //
// Project: FlappyBird
// Start: Tuesday, May 12, 2015
// IDE Version: 12.312

TYPE tplayer
	x; y
	vX; vY
	livePoints
	playerSize = 32
	playerSet$ = "32x32ninja_m.png"
	fhPlayerSet

	FUNCTION init: x, y
		self.x = x
		self.y = y

		self.vX = 0
		self.vY = 0

		self.fhPlayerSet = GENSPRITE()
		LOADANIM self.playerSet$, self.fhPlayerSet, self.playerSize, self.playerSize
	ENDFUNCTION

	FUNCTION update:	
		// Bewegung
		IF KEY(203) THEN DEC self.vX, 0.7 //nach links bewegen
		IF KEY(205) THEN INC self.vX, 0.7 //nach rechts bewegen
		IF KEY(57)
			self.vY = - 8 //huepfen
		ENDIF		
		
		// Traegheit
		self.vX = self.vX * 0.85
				
		// Player Bewegen und auf Kollision Pruefen
		INC self.x, self.vX
		// Pruefen auf Kollision mit der Map
		IF map.checkCollision(getPositionX(), getPositionY(), 1)
			DEC self.x, self.vX
		ENDIF

		INC self.y, self.vY			
		// Pruefen auf Kollision mit der Map
		IF map.checkCollision(getPositionX(), getPositionY(), 1)								
			DEC self.y, self.vY		
			DEC self.vY, 1.5			
		ELSE
			// Schwerkraft
			INC self.vY, 1.5	
		ENDIF
		
		// Scrollen der Map (aber nur in X Richtung)
		LOCAL sx, sy
		GETSCREENSIZE sx, sy
		
		// Rechts Bewegung nach Links Scrollen, in der Mitte der Seite
		IF getPositionX() >= (sx / map.getTileSize() / 2) AND KEY(205) 
			map.setScrollX(-0.2)
			DEC self.x, (self.vX / 0.85)
		ENDIF
		
		// Links Bewegung nach Rechts Scrollen, in der linken hälfte der Seite
		//IF getPositionX() <= (sx / map.getTileSize() / 3) AND KEY(203)
		//	map.setScrollX(0.5)
		//	DEC self.x, (self.vX / 0.85)
		//ENDIF
		
	ENDFUNCTION

	FUNCTION render:
		DRAWANIM self.fhPlayerSet, 1, self.x, self.y
	ENDFUNCTION

	// Funktion: getPositionX
	// Gibt die Position X auf der Map aus
	// Return: X Position
	FUNCTION getPositionX:
		RETURN INTEGER(self.x / map.getTileSize() + 0.5)
	ENDFUNCTION

	// Funktion: getPositionY
	// Gibt die Position Y auf der Map aus
	// Return: Y Position
	FUNCTION getPositionY:
		RETURN INTEGER(self.y / map.getTileSize() + 1)
	ENDFUNCTION


ENDTYPE





